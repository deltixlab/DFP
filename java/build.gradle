plugins {
    id "me.champeau.gradle.jmh" version "0.5.0" apply false
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'me.champeau.gradle.jmh'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDir "$rootDir/java/src/main/java"
            srcDir "$rootDir/NativeUtils/java/src/main/java"
            srcDir "$rootDir/NativeUtils/Zstandard/java/src/main/java"

            resources {
                srcDirs = ["$rootDir/native-gcc/bin/Release/"]
                include '**/*_so.zst'
                include '**/*.so.zst'
                include '**/*.dylib.zst'
                include '**/*.dll.zst'
            }
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testImplementation 'junit:junit:4.+'
    integrationTestCompile 'junit:junit:4.+'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//compileJava.dependsOn(":nativeWrappers:makeNativeWrappers")

publishing {
    repositories {
        maven {
            url "${System.getenv('ARTIFACTORY_URL')}/${System.getenv('FEED_BASE_NAME') ?: 'EPM-RTC-test'}-java"
            credentials {
                username System.getenv('ARTIFACTORY_USER') ?: ''
                password System.getenv('ARTIFACTORY_PASS') ?: ''
            }
        }
    }

    publications {
        shadow(MavenPublication) { publication ->
            from components.java
            artifactId project.name
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Deltix Decimal Floating-Point Arithmetic Library',
            'Implementation-Version': version
    }
    setEntryCompression ZipEntryCompression.STORED
}

jmh {
    include = '.*Benchmark.*'
}

test.outputs.upToDateWhen { false }

task integrationTest(type: Test) {
    group = 'verification'
    systemProperty "com.epam.deltix.dfp.dataFiles.parsingAndFormatting", "$projectDir/data/Decimal64.FormattingAndParsing.txt"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    // Assume that test are never up-to-date.
    outputs.upToDateWhen { false }
}

// Ensures that test reports for unit tests and integrations tests are kept separate.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

